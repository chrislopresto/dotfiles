#compdef pgrep pkill 
#
# Adapted for use on Darwin, on which I have pgrep and pkill via
# MacPorts (proctools), but the default ZSH completion script barfs on:
#
#  % pkill ^Ips: illegal argument: co
#  usage: ps [-AaCcEefhjlMmrSTvwXx] [-O fmt | -o fmt] [-G gid[,gid...]]
#            [-g grp[,grp...]] [-u [uid,uid...]]
#            [-p pid[,pid...]] [-t tty[,tty...]] [-U user[,user...]]
#         ps [-L]
#
# XXX a better fix would be to make the default _pgrep completion use
# zstyle or some other means of selecting the appropriate platform-
# specific ps call as necessary.

local context state line ret=1 expl
typeset -A opt_args
typeset -a arguments

arguments=('-P[parent process id]:parent process id:->ppid' 
     '-g[match only in process group ids]:group:->pgid' 
     '-G[match only real group id]:group:_groups' 
     '-t[match only controlled by terminal]:terminal device:->tty'
     '-u[match only effective user id]:user:_users' 
     '-U[match only real user id]:user:_users' 
     '-n[newest process]' 
     '-f[match against full command line]' 
     '-v[negate matching]' 
     '-x[match exactly]' 
     '*:process name:->pname')

if [[ $service == 'pkill' ]]
then
  arguments+=('-'${^signals}'[signal]')
elif [[ $service == 'pgrep' ]]
then
  arguments+=('-d[output delimiter]:delimiter:compadd ${(s\:\:)IFS}'
        '-l[list name in addition to id]')
fi

_arguments -s -w $arguments && ret=0

case $state in
  (tty)
    compset -P '*,'

    local -a used ttys
    used=(${(s:,:)IPREFIX})

    ttys=( /dev/tty*(N) )
    _wanted tty expl 'terminal device' compadd -S ',' -q -F used ${ttys#/dev/}
    ;;
    
  (ppid)
    compset -P '*,'

    local -a used ppid
    used=(${(s:,:)IPREFIX})
    ppid=(${(uon)$(ps -Ao ppid=)})

    _wanted ppid expl 'parent process id' compadd -S ',' -q -F used $ppid
    ;;

  (pgid)
    compset -P '*,'

    local -a used pgid
    used=(${(s:,:)IPREFIX})
    pgid=(${(uon)$(ps -Ao pgid=)})

    _wanted pgid expl 'process group id' compadd -S ',' -q -F used $pgid
    ;;
  
  (pname)
    local ispat="pattern matching "
    if (( ${+opt_args[-x]} ))
    then
      ispat=""
    fi
    if (( ${+opt_args[-f]} ))
    then
      _wanted pname expl $ispat'process command line' compadd ${(u)${(f)"$(ps -Ao command)"}}
    else
      _wanted pname expl $ispat'process name' compadd ${(u)${(f)"$(ps -Aco command)"}}
    fi
    ;;
  
esac && ret=0

return ret
